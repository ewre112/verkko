#############################################################################
 #
 #  This file is part of Verkko, a software program that assembles
 #  whole-genome sequencing reads into telomere-to-telomere
 #  haplotype-resolved chromosomes.
 #
 #  Except as indicated otherwise, this is a 'United States Government
 #  Work', and is released in the public domain.
 #
 #  File 'README.licenses' in the root directory of this distribution
 #  contains full conditions and disclaimers.
 #
 ##

#
#  Rule processGraph massages the MBG output into initial unitigs.
#
#  This is rule process_graph in Snakefile in the original.
#
#  Configuration Parameters:
#    None
#

rule processGraph:
    input:
        gfa   = rules.buildGraph.output.graph,
        paths = rules.buildGraph.output.paths,
        correctedHifi = rules.correctHiFi.output.fasta
    output:
        gfa = '2-processGraph/unitig-unrolled-hifi-resolved.gfa',
        map = '2-processGraph/unitig-mapping-1.txt'
    log:
        err = '2-processGraph/process.err'
    threads:
        int(config['utg_n_cpus'])
    resources:
        job_id = 1,
        n_cpus = config['utg_n_cpus'],
        mem_gb = lambda wildcards, input, attempt: getMemoryRequest(attempt, 'utg'),
        time_h = lambda wildcards, input, attempt: getTimeRequest(attempt, 'utg')
    shell:
        '''
cd 2-processGraph

cat > ./processGraph.sh <<EOF
#!/bin/sh
set -e

echo Gap insertion pass 1.
{PYTHON} {VERKKO}/scripts/insert_aln_gaps.py ../{input.gfa} ../{input.paths} 2 50 paths-nogap-1.gaf gaps-hifi-1.gaf gapone n y ../{input.correctedHifi} \\\\
  > gapped-once-hifi-resolved.gfa

echo ""
echo Gap insertion pass 2.
{PYTHON} {VERKKO}/scripts/insert_aln_gaps.py gapped-once-hifi-resolved.gfa paths-nogap-1.gaf 2 300 paths-nogap-2.gaf gaps-hifi-2.gaf gaptwo n y ../{input.correctedHifi} \\\\
  > gapped-twice-hifi-resolved.gfa

echo ""
echo Gap insertion pass 3.
{PYTHON} {VERKKO}/scripts/insert_aln_gaps.py gapped-twice-hifi-resolved.gfa paths-nogap-2.gaf 1 5 paths-nogap-3.gaf gaps-hifi-3.gaf gapthree n y ../{input.correctedHifi} \\\\
  > gapped-thrice-hifi-resolved.gfa

echo ""
echo Gap insertion pass 4.
{PYTHON} {VERKKO}/scripts/chop_misassemblies.py gapped-thrice-hifi-resolved.gfa paths-nogap-3.gaf chop-map.txt paths-cut.gaf 3 0 \\\\
  > chopped-hifi-resolved.gfa
{PYTHON} {VERKKO}/scripts/maybe_trim_alignments.py chopped-hifi-resolved.gfa 0 \\\\
  < paths-cut.gaf \\\\
  > paths-cut-trim.gaf
{PYTHON} {VERKKO}/scripts/insert_aln_gaps.py chopped-hifi-resolved.gfa paths-cut-trim.gaf 2 0 paths-nogap-4.gaf gaps-hifi-4.gaf gapfour y y ../{input.correctedHifi} \\\\
  > gapped-hifi-resolved.gfa

echo ""
echo Tip unrolling.
cut -f 6 < paths-nogap-4.gaf \\\\
| \\\\
{PYTHON} {VERKKO}/scripts/unroll_tip_loops.py gapped-hifi-resolved.gfa 2 \\\\
  > unrolled-hifi-resolved.gfa

{PYTHON} {VERKKO}/scripts/get_unroll_mapping.py gapped-hifi-resolved.gfa unrolled-hifi-resolved.gfa \\\\
  > unroll_mapping_1.txt

echo ""
echo Unitig creation.
{PYTHON} {VERKKO}/scripts/unitigify.py utig1- ../{output.map} \\\\
  < unrolled-hifi-resolved.gfa \\\\
  > ../{output.gfa}
EOF

chmod +x ./processGraph.sh

./processGraph.sh > ../{log.err} 2>&1
        '''
